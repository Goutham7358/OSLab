alias userSP R5;
userSP=SP;
print("INT11");
[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1]*16) + 13] = SP;        // saving value of SP => USER SP 
SP = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1]*16) + 11]*512 - 1; // SP=>beginning of user area page

alias sysCallNum R6;
sysCallNum = [([PTBR + 2*(userSP-5)/512]*512) + ((userSP-5)%512)]; // getting syscall from user stack

[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 9] = sysCallNum; //set the MODEFLAG with syscallnum  

alias processTAddr R7;
processTAddr = PROCESS_TABLE +[SYSTEM_STATUS_TABLE+1]*16;

alias physicalAddrRetVal R8;
physicalAddrRetVal = ([PTBR + 2*(userSP-1)/512]*512) + ((userSP-1)%512);

if(sysCallNum==11) then // Getpid
	[physicalAddrRetVal] = [processTAddr +1];
endif;

if(sysCallNum==12) then //Getppid
	[physicalAddrRetVal] = [processTAddr +2];
endif;

if(sysCallNum==13) then //Wait
	print("Gonna wait!");
	alias waitPID R9;
	waitPID = [([PTBR + 2*(userSP-4)/512]*512) + ((userSP-4)%512)];
	if((waitPID<0)||(waitPID>15)) then  // waitPID not in valid range
		[physicalAddrRetVal]=-1;
		[processTAddr +9]=0;
		SP=[processTAddr+13];
		ireturn;
	endif;
	
	if(waitPID==[SYSTEM_STATUS_TABLE+1]) then  // waiting for itself
		[physicalAddrRetVal]=-1;
		[processTAddr +9]=0;
		SP=[processTAddr+13];
		ireturn;
	endif;
	
	if([PROCESS_TABLE+ waitPID*16 +4]==TERMINATED) then  // waiting for terminated process
		[physicalAddrRetVal]=-1;
		[processTAddr +9]=0;
		SP=[processTAddr+13];
		ireturn;
	endif;
	
	[processTAddr +4] = WAIT_PROCESS;
	[processTAddr +5] = waitPID;
	
	
	multipush(R0,R1,R2,R3,R4,R5,R6,R7,R8,R9);
	call MOD_5;
	multipop(R0,R1,R2,R3,R4,R5,R6,R7,R8,R9);
	

	[physicalAddrRetVal]=0;
			
endif;

if(sysCallNum==14) then //Signal
	alias counter R4;
	counter=0;
	while(counter<16) do
		if(([PROCESS_TABLE+counter*16+4]==WAIT_PROCESS) && ([PROCESS_TABLE +counter*16 +5]==[SYSTEM_STATUS_TABLE+1])) then
			[PROCESS_TABLE+counter*16+4]=READY;
		endif;
		counter=counter+1;
	endwhile;
	
	[physicalAddrRetVal]=0;

endif;

[PROCESS_TABLE + [SYSTEM_STATUS_TABLE+1]*16 +9]=0;
SP=[PROCESS_TABLE+([SYSTEM_STATUS_TABLE+1]*16)+13];
ireturn;
